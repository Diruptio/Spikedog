plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.24.0'
    id 'maven-publish'
}

version = "1.2.3"
group = "diruptio"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains:annotations:24.1.0"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.yaml:snakeyaml:2.2"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Implementation-Title': 'Spikedog',
                   'Implementation-Version': version,
                   'Main-Class': 'diruptio.spikedog.Spikedog'
    }
    archiveFileName = 'Spikedog.jar'
}

def generateSources = tasks.register('generateSources', Copy) {
    from file('src/main/templates')
    into layout.buildDirectory.dir('generated/sources/templates')
    expand 'version': version
}
sourceSets.main.java.srcDir(generateSources.map { it.outputs })
compileJava.dependsOn generateSources

application {
    mainClass = jar.manifest.attributes['Main-Class'] as String
    run.workingDir = 'run'
    run.workingDir.mkdirs()
}

spotless {
    java {
        target '**/src/**/*.java'
        googleJavaFormat().aosp()
        removeUnusedImports()
        indentWithSpaces()
        endWithNewline()
    }
}

publishing {
    repositories {
        maven {
            url = "https://repo.diruptio.de/repository/maven-public-${version.endsWith("SNAPSHOT") ? "snapshots" : "releases"}/"
            name = "DiruptioPublic"
            credentials {
                username = findProperty("maven_username") ?: System.getenv("MAVEN_USERNAME")
                password = findProperty("maven_password") ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}